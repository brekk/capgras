import type { Either } from "Either"
import type { Maybe } from "Maybe"

import type { Dependency } from "@/Dependency"

import { Left, Right } from "Either"
import Json from "Json/Parse"



export alias MadJson = {
  dependencies :: Maybe (List Dependency),
  name :: String,
  version :: String,
}

__json :: String -> String -> Maybe (List Dependency) -> MadJson
__json = (name, version, dependencies) => ({ name, version, dependencies })

__dependency :: String -> String -> String -> Dependency
__dependency = (minVersion, url, description) => ({ minVersion, url, description })

madJson :: Json.Parser MadJson
export madJson = Json.map3(
  __json,
  Json.field("name", Json.string),
  Json.field("version", Json.string),
  Json.maybe(
    Json.field(
      "dependencies",
      Json.list(
        Json.map3(
          __dependency,
          Json.field("minVersion", Json.string),
          Json.field("url", Json.string),
          Json.field("description", Json.string),
        ),
      ),
    ),
  ),
)

export type MadPackage = Unparsed(String) | ParsedMadPackage(MadJson)

unpack :: Either String MadJson -> MadPackage
export unpack = where {
  Left(l) =>
    Unparsed(l)

  Right(r) =>
    ParsedMadPackage(r)
}
